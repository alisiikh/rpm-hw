import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "nebula.rpm" version "4.8.0"
}

group 'com.alisiikh'
version '1.0'

ext {
    rpm = [
            buildDir   : "$buildDir/tmp/buildRpm",
            pkgName    : project.name,
            pkgVersion : project.version,
            pkgRelease : '1',
            pkgUser    : 'root',
            pkgGroup   : 'root',
            pkgDirMode : 0644,
            pkgFileMode: 0755
    ]
}

task prepareRpm(type: Copy, group: 'rpm', description: 'Prepares resources for RPM build.') {
    from('rpm/script') {
        filter(ReplaceTokens, tokens: rpm)
    }
    into("${rpm.buildDir}/script")
}
task buildRpm(type: Rpm, dependsOn: 'prepareRpm', group: 'rpm', description: 'Builds RPM package.') {
    packageName = rpm.pkgName
    version = "${rpm.pkgVersion}.${rpm.pkgRelease}"
    release = rpm.pkgRelease
    arch = NOARCH
    os = LINUX

    user = rpm.pkgUser
    packageGroup = rpm.pkgGroup

    dirMode = rpm.pkgDirMode as int
    fileMode = rpm.pkgFileMode as int

    preInstall file("${rpm.buildDir}/script/preinstall.sh")
    postInstall file("${rpm.buildDir}/script/postinstall.sh")

    requires('bash')

    from('rpm/bin') {
        into("/usr/bin")
    }
}
