import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "nebula.ospackage" version "4.8.0"
}

group 'com.alisiikh'
version '1.0'

ext {
    rpm = [
            tmpDir    : "$buildDir/tmp/buildRpm",
            pkgName   : project.name,
            pkgVersion: project.version,
            pkgRelease: '1',

            pkgUser   : 'root',
            pkgGroup  : 'root'
    ]
}

task prepareRpm(type: Copy, group: 'rpm', description: 'Prepares resources for RPM build.') {
    from('script') {
        filter(ReplaceTokens, tokens: rpm)
    }
    into("${rpm['tmpDir']}/script")
}
ospackage {
    packageName = rpm['pkgName']
    version = "${rpm['pkgVersion']}.${rpm['pkgRelease']}"
    release = rpm['pkgRelease']
    arch = NOARCH
    os = LINUX

    user = rpm['pkgUser']
    packageGroup = rpm['pkgGroup']

    preInstall file("${rpm.tmpDir}/script/preinstall.sh")

    requires('bash')
}
buildRpm {
    group ='rpm'
    description = 'Builds RPM package.'

    dependsOn 'prepareRpm'

    from('bin') {
        include 'hw'
        into("/usr/bin")
        fileMode = 0755
    }
    from('bin/etc') {
        into("/opt/hw")
        fileMode = 0644
    }
}
tasks.remove(buildDeb)
